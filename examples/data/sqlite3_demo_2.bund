//
// This is demonstration of how you can query data stored in
// SQLite3 database, perform data analysis and display results
// In this demo we are using PRQL rather than SQL which supports transparently
//

//
// Store document name for fmt conditional
//
:index
// Add list to the workbench
list .
//
// Prepare SQLite3 database filename in the stack
//
cwd "{A}/examples/data_sets/sample.db" format dup
"Processing data from {A}" format println
//
// And create SQLITE conditional
//
sqlite
  //
  // Skip the first row
  //
  "skip_first" 1 set
  //
  // Run this PRQL query, that is equivalent to "select * from sample"
  //
  :prql "from sample" set
  //
  // And execute this lambda on result
  //
  :lambda {
      // Get the score, add to the list in the workbench and drop the row
      :Score swap get, ++. drop
  } set
//
// Execute SQLITE conditional
//
!
// Set shape for sample.analysis
4.0
// Perform Markov chain forecasting and store result in workbench
take dup forecast.markov .
// Perform analysis of the sample
sample.analysis
  // And add results of Markov forecast
  :next.markov take set
//
// Create fmt conditional
//
fmt
  :index cwd "{A}/examples/data/sqlite3_demo_1.md" format file ,
//
// Merge fmt with result of analysis and display it
//
++ display
